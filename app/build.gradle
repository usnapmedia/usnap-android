buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}


/*
 * Manually set the MAJOR, MINOR and PATCH version components here.
 */
ext {
    majorVersion = "0"
    minorVersion = "1"
    patchVersion = "0"
}

def computeVersionName() {
    return majorVersion + "." + minorVersion + "." + patchVersion
}

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"
    defaultConfig {
        applicationId "com.samsao.snapzi"
        minSdkVersion 14
        targetSdkVersion 22
        versionCode System.env.BUILD_NUMBER ? "$System.env.BUILD_NUMBER".toInteger() : 999999
        versionName computeVersionName()
        buildConfigField "String", "PROD", "\"prod\""
        buildConfigField "String", "DEV", "\"dev\""
    }

    signingConfigs {
        debug {
            storeFile file("../android.keystore")
            storePassword "zJTc7eVt1gWSIfCPQ85O"
            keyAlias "debug"
            keyPassword "7G5ubnVJXEMXhtwkaCN0"
        }
        release {
            storeFile file("../android.keystore")
            storePassword "zJTc7eVt1gWSIfCPQ85O"
            keyAlias "release"
            keyPassword "g8bMpVsfg9NEkVENpGbO"
        }
    }

    buildTypes {
        debug {
            debuggable true
            signingConfig signingConfigs.debug
        }
        release {
            debuggable false
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            ext.betaDistributionGroupAliases="snapzi-android"
        }
        debugRelease.initWith(buildTypes.release)
        debugRelease {
            debuggable true
        }
    }

    lintOptions {
        disable 'InvalidPackage'
        abortOnError false
    }

    packagingOptions {
        exclude 'android.keystore'
        exclude 'creativesdk-repo/*'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude '.readme'
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    dexOptions {
        javaMaxHeapSize "4g"
    }

    productFlavors {
        prod {
            buildConfigField "String", "ENV", "\"prod\""
        }

        dev {
            buildConfigField "String", "ENV", "\"dev\""
            versionName computeVersionName() + "-DEV"
        }
    }

    sourceSets {
        dev.setRoot('src/dev')
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.google.android.gms:play-services-identity:6.5.87'
    compile 'com.google.android.gms:play-services-plus:6.5.87'
    compile 'com.android.support:appcompat-v7:22.0.0'
    compile 'com.jakewharton:butterknife:6.1.0'
    compile 'com.github.frankiesardo:icepick:2.3.6'
    provided 'com.github.frankiesardo:icepick-processor:2.3.6'
    compile('com.crashlytics.sdk.android:crashlytics:2.2.2@aar') {
        transitive = true;
    }
    compile('com.twitter.sdk.android:twitter:1.3.2@aar') {
        transitive = true;
    }
    compile 'com.facebook.android:facebook-android-sdk:3.21.1'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'jp.wasabeef:picasso-transformations:1.0.2@aar'
    compile 'jp.co.cyberagent.android.gpuimage:gpuimage-library:1.2.3@aar'
    compile 'me.panavtec:drawableview:0.5.0'
    compile 'com.afollestad:material-dialogs:0.6.5.1'
    compile 'com.larswerkman:HoloColorPicker:1.5'
}
